/*
 * Copyright (c) 2022 - present. New Relic Corporation. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    repositories {
        maven {
            name "artifacts"
            url uri(rootProject.mavenLocal)
        }
    }

    publications {
        libraryProject(MavenPublication) {
            groupId 'com.newrelic.agent.android'
            version project.versions.agent

            pom {
                name = 'com.newrelic.agent.android'
                description = 'The New Relic Android Agent provides run time performance monitoring for Android applications'
                url = 'https://newrelic.com'
                packaging = 'aar'

                licenses {
                    license {
                        url = 'https://newrelic.com/docs/java/java-agent-license'
                    }
                }

                developers {
                    developer {
                        id = 'mobile-agents-team'
                        name = 'Mobile Agents Team'
                        email = 'mobile-agents@newrelic.com'
                    }
                }

                scm {
                    url = 'scm:git:https://github.com/newrelic/newrelic-android-agent.git'
                    connection = 'scm:git:https://github.com/newrelic/newrelic-android-agent.git'
                }

                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each { dependency ->
                        if (dependency.group != null && dependency.name != null && dependency.version != null && !dependency.name.contains("agent-core")) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dependency.group)
                            dependencyNode.appendNode('artifactId', dependency.name)
                            dependencyNode.appendNode('version', dependency.version)
                            dependencyNode.appendNode('scope', 'runtime')
                        }
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            project.findProperty("signingKeyId"),
            project.findProperty("signingKey"),
            project.findProperty("signingPassword")
    )
    sign publishing.publications.libraryProject
    required {
        gradle.taskGraph.hasTask("publishAllPublicationsToArtifactsRepository") ||
                gradle.taskGraph.hasTask("publishAllPublicationsToSnapshotsRepository")
    }
}

project.tasks.register("install") {
    dependsOn publishLibraryProjectPublicationToMavenLocal
    description "Publishes all Maven publications produced by this project to the local Maven cache. " +
            "Replacement for the legacy 'maven' plugin task."
    group "publishing"
}

