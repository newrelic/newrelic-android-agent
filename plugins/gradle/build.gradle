/*
 * Copyright (c) 2022 - present. New Relic Corporation. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id("groovy")
    id("org.jetbrains.kotlin.jvm")
}

import org.gradle.api.internal.classpath.ModuleRegistry
import org.gradle.api.internal.project.ProjectInternal


group = 'com.newrelic.agent.android'
version = project.versions.agent

sourceSets {
    main.java.srcDirs += "src/main/kotlin"
}

compileGroovy {
    dependsOn tasks.getByPath('compileKotlin')
    classpath += files(compileKotlin.destinationDir)
}

compileKotlin {
    classpath += files(compileGroovy.destinationDir)
}

classes.dependsOn compileKotlin

dependencies {
    implementation project(path: ':instrumentation', configuration: 'shadow')
    implementation files("${System.getProperty('java.home')}/../lib/tools.jar")

    compileOnly 'com.android.tools.build:gradle:' + project.versions.agp.plugin
    compileOnly 'com.android.tools.build:gradle-api:' + project.versions.agp.gradleApi
    compileOnly gradleApi()

    implementation 'commons-io:commons-io:' + project.versions.java.commonsIO
    implementation 'com.guardsquare:proguard-gradle:' + project.versions.java.proguard
    implementation 'org.ow2.asm:asm:' + project.versions.java.asm
    implementation 'org.ow2.asm:asm-util:' + project.versions.java.asm

    compileOnly localGroovy()
    compileOnly 'com.google.guava:guava:' + project.versions.java.guava

    testImplementation 'com.android.tools.build:gradle:' + project.versions.agp.plugin
    testImplementation 'com.android.tools.build:gradle-api:' + project.versions.agp.gradleApi

    testImplementation gradleApi()
    testImplementation gradleTestKit()

    testImplementation project(path: ':instrumentation', configuration: 'default')
    testImplementation 'junit:junit:' + project.versions.test.junit
    testImplementation 'org.junit.jupiter:junit-jupiter:' + project.versions.test.jupiter
    testImplementation 'org.junit.vintage:junit-vintage-engine:' + project.versions.test.jupiter
    testImplementation 'org.mockito:mockito-core:' + project.versions.test.mockitoCore
    testImplementation 'com.google.guava:guava:' + project.versions.java.guava
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }

    def toolingApiBuildersJar = (project as ProjectInternal).services.get(ModuleRegistry.class)
            .getModule("gradle-tooling-api-builders")
            .classpath
            .asFiles
            .first()

    testRuntimeOnly(files(toolingApiBuildersJar))
}

tasks.withType(JavaCompile).configureEach {
    dependsOn(":instrumentation:jar")
}

tasks.named("compileKotlin").configure {
    dependsOn(":instrumentation:jar")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform() {
        includeEngines("junit-jupiter")
    }

    ["integrationTests", "regressionTests", "newrelic.agent.args", "newrelic.agent.version"].each {
        if (project.hasProperty(it)) {
            systemProperty it, project.property(it)
        }
    }

    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
        jvmArgs(["--add-opens=java.base/java.lang=ALL-UNNAMED",
                 "--add-opens=java.base/java.util=ALL-UNNAMED"])
    }
}

test {
    useJUnitPlatform()
    maxHeapSize = '4G'
    onlyIf {
        project.hasProperty('regressionTests')  // || project.hasProperty('integrationTests') ||
    }
}

artifacts {
    archives jar
}


apply from: "${rootDir}/gradle/publishing.gradle"

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'agent-gradle-plugin'
            pom {
                artifactId = 'agent-gradle-plugin'
                name = 'New Relic Android Gradle Plugin'
                description = 'The New Relic Android Gradle plugin instruments classes for Android applications'
            }
        }
    }
}
