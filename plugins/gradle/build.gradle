/*
 * Copyright (c) 2022 - present. New Relic Corporation. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id("groovy")
    id("org.jetbrains.kotlin.jvm")
}

import org.gradle.api.internal.classpath.ModuleRegistry
import org.gradle.api.internal.project.ProjectInternal


group = 'com.newrelic.agent.android'
version = project.versions.agent

sourceSets {
    main.java.srcDirs += "src/main/kotlin"
}

def integrationTest = sourceSets.create("integrationTest")
def functionalTest = sourceSets.create("functionalTest")

compileGroovy {
    dependsOn tasks.getByPath('compileKotlin')
    classpath += files(compileKotlin.destinationDir)
}

compileKotlin {
    classpath += files(compileGroovy.destinationDir)
}

classes.dependsOn compileKotlin

dependencies {
    implementation project(path: ':instrumentation', configuration: 'shadow')
    implementation files("${System.getProperty('java.home')}/../lib/tools.jar")

    compileOnly 'com.android.tools.build:gradle:' + project.versions.agp.plugin
    compileOnly 'com.android.tools.build:gradle-api:' + project.versions.agp.gradleApi
    compileOnly gradleApi()

    implementation 'commons-io:commons-io:' + project.versions.java.commonsIO
    implementation 'com.guardsquare:proguard-gradle:' + project.versions.java.proguard
    implementation 'org.ow2.asm:asm:' + project.versions.java.asm
    implementation 'org.ow2.asm:asm-util:' + project.versions.java.asm

    compileOnly localGroovy()
    compileOnly 'com.google.guava:guava:' + project.versions.java.guava

    testImplementation(project)
    testImplementation project(path: ':instrumentation', configuration: 'default')
    testImplementation gradleApi()
    testImplementation gradleTestKit()
    testImplementation 'com.android.tools.build:gradle:' + project.versions.agp.plugin
    testImplementation 'com.android.tools.build:gradle-api:' + project.versions.agp.gradleApi
    testImplementation 'commons-io:commons-io:' + project.versions.java.commonsIO
    testImplementation 'com.google.guava:guava:' + project.versions.java.guava
    testImplementation 'junit:junit:' + project.versions.test.junit
    testImplementation 'org.junit.jupiter:junit-jupiter:' + project.versions.test.jupiter
    testImplementation 'org.junit.vintage:junit-vintage-engine:' + project.versions.test.jupiter
    testImplementation 'org.mockito:mockito-core:' + project.versions.test.mockitoCore
    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }

    // integrations
    integrationTestImplementation(project)
    integrationTestImplementation project(path: ':instrumentation', configuration: 'default')
    integrationTestImplementation gradleApi()
    integrationTestImplementation gradleTestKit()
    integrationTestImplementation 'com.android.tools.build:gradle:' + project.versions.agp.plugin
    integrationTestImplementation 'com.android.tools.build:gradle-api:' + project.versions.agp.gradleApi
    integrationTestImplementation 'commons-io:commons-io:' + project.versions.java.commonsIO
    integrationTestImplementation 'com.google.guava:guava:' + project.versions.java.guava
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter:' + project.versions.test.jupiter
    integrationTestImplementation 'org.junit.vintage:junit-vintage-engine:' + project.versions.test.jupiter
    integrationTestImplementation 'org.mockito:mockito-core:' + project.versions.test.mockitoCore
    integrationTestImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    integrationTestImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }

    // regressions
    functionalTestImplementation(project)
    functionalTestImplementation project(path: ':instrumentation', configuration: 'default')
    functionalTestImplementation gradleApi()
    functionalTestImplementation gradleTestKit()
    functionalTestImplementation 'com.android.tools.build:gradle:' + project.versions.agp.plugin
    functionalTestImplementation 'com.android.tools.build:gradle-api:' + project.versions.agp.gradleApi
    functionalTestImplementation 'commons-io:commons-io:' + project.versions.java.commonsIO
    functionalTestImplementation 'com.google.guava:guava:' + project.versions.java.guava
    functionalTestImplementation 'org.junit.jupiter:junit-jupiter:' + project.versions.test.jupiter
    functionalTestImplementation 'org.junit.vintage:junit-vintage-engine:' + project.versions.test.jupiter
    functionalTestImplementation 'org.mockito:mockito-core:' + project.versions.test.mockitoCore
    functionalTestImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    functionalTestImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }

    def toolingApiBuildersJar = (project as ProjectInternal).services.get(ModuleRegistry.class)
            .getModule("gradle-tooling-api-builders")
            .classpath
            .asFiles
            .first()

    testRuntimeOnly(files(toolingApiBuildersJar))
}

tasks.withType(JavaCompile).configureEach {
    dependsOn(":instrumentation:jar")
}

tasks.named("compileKotlin").configure {
    dependsOn(":instrumentation:jar")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform() {
        includeEngines("junit-jupiter")
    }

    ["newrelic.agent.args", "newrelic.agent.version"].each {
        if (project.hasProperty(it)) {
            systemProperty it, project.property(it)
        }
    }

    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
        jvmArgs(["--add-opens=java.base/java.lang=ALL-UNNAMED",
                 "--add-opens=java.base/java.util=ALL-UNNAMED"])
    }
}

test {
    useJUnitPlatform() {
        includeEngines("junit-jupiter")
    }
    maxHeapSize = '4G'
}

def integrationTestTask = tasks.register("integrationTests", Test) {
    description = 'Runs the integration tests.'
    group = "verification"
    testClassesDirs = integrationTest.output.classesDirs
    classpath = integrationTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}

def functionalTestTask = tasks.register("functionalTests", Test) {
    description = 'Runs the functional/regression tests.'
    group = "verification"
    testClassesDirs = functionalTest.output.classesDirs
    classpath = functionalTest.runtimeClasspath
    mustRunAfter(tasks.named('test'))
}

tasks.named('check') {
    dependsOn(functionalTestTask)
}

artifacts {
    archives jar
}

apply from: "${rootDir}/gradle/publishing.gradle"

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'agent-gradle-plugin'
            pom {
                artifactId = 'agent-gradle-plugin'
                name = 'New Relic Android Gradle Plugin'
                description = 'The New Relic Android Gradle plugin instruments classes for Android applications'
            }
        }
    }
}
