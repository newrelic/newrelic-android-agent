/*
 * Copyright (c) 2022 - present. New Relic Corporation. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id("groovy")
    id("org.jetbrains.kotlin.jvm")
}

group = 'com.newrelic.agent.android'
version = project.versions.agent

sourceSets {
    main.java.srcDirs += "src/main/kotlin"
}

compileGroovy {
    dependsOn tasks.getByPath('compileKotlin')
    classpath += files(compileKotlin.destinationDir)
}

compileKotlin {
    classpath += files(compileGroovy.destinationDir)
}

classes.dependsOn compileKotlin

dependencies {
    implementation project(path: ':instrumentation', configuration: 'shadow')
    implementation files("${System.getProperty('java.home')}/../lib/tools.jar")
    implementation 'commons-io:commons-io:' + project.versions.java.commonsIO

    implementation 'com.android.tools.build:gradle:' + project.versions.agp.gradleApi
    implementation 'com.android.tools.build:gradle-api:' + project.versions.agp.gradleApi
    implementation gradleApi()

    implementation 'com.guardsquare:proguard-gradle:7.3.1'
    implementation 'org.ow2.asm:asm:' + project.versions.java.asm
    implementation 'org.ow2.asm:asm-util:' + project.versions.java.asm

    compileOnly localGroovy()
    compileOnly 'com.google.guava:guava:' + project.versions.java.guava

    testImplementation 'dev.gradleplugins:gradle-test-kit:' + project.versions.agp.gradleTestKit
    testImplementation project(path: ':instrumentation', configuration: 'default')
    testImplementation 'junit:junit:' + project.versions.test.junit
    testImplementation 'org.junit.jupiter:junit-jupiter:' + project.versions.test.jupiter
    testImplementation 'org.junit.vintage:junit-vintage-engine:' + project.versions.test.jupiter
    testImplementation 'org.mockito:mockito-core:' + project.versions.test.mockitoCore
    testImplementation 'com.google.guava:guava:' + project.versions.java.guava
    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
}

tasks.withType(JavaCompile).configureEach {
    dependsOn(":instrumentation:jar")
}

tasks.named("compileKotlin").configure {
    dependsOn(":instrumentation:jar")
}

tasks.withType(Test).configureEach {
    useJUnitPlatform() {
        includeEngines("junit-jupiter")
    }

    ["integrationTests", "regressionTests", "newrelic.agent.args", "newrelic.agent.version"].each {
        if (project.hasProperty(it)) {
            systemProperty it, project.property(it)
        }
    }
}

test {
    useJUnitPlatform()
    maxHeapSize = '2G'
    onlyIf {
        false   // FIXME project.hasProperty('integrationTests') || project.hasProperty('regressionTests')
    }
}

artifacts {
    archives jar
}


apply from: "${rootDir}/gradle/publishing.gradle"

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId 'agent-gradle-plugin'
            pom {
                artifactId = 'agent-gradle-plugin'
                name = 'New Relic Android Gradle Plugin'
                description = 'The New Relic Android Gradle plugin instruments classes for Android applications'
            }
        }
    }
}
